package tween;

import haxe.Constraints.Function;
import js.Browser;
import js.Syntax;
import js.lib.Error;
import tools.NativeJS;
import tween.Ease;

/**
 * –¢–≤–∏–Ω–µ—Ä. üèÉ  
 * –î–ª—è –æ–±—â–µ–≥–æ –ø–æ–Ω–∏–º–∞–Ω–∏—è —Ç–≤–∏–Ω–µ—Ä –º–æ–∂–Ω–æ –ø—Ä–µ–¥—Å—Ç–∞–≤–∏—Ç—å
 * —Å–µ–±–µ, –∫–∞–∫ –Ω–µ–±–æ–ª—å—à–æ–π –ø–ª–µ–µ—Ä –º–∏–∫—Ä–æ–ø—Ä–æ–≥—Ä–∞–º–º—ã,
 * –∫–æ—Ç–æ—Ä—É—é –≤—ã –µ–º—É –∑–∞–¥–∞—ë—Ç–µ. –í–æ –≤—Ä–µ–º—è –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è
 * —Ç–≤–∏–Ω–µ—Ä –∏–¥—ë—Ç —à–∞–≥ –∑–∞ —à–∞–≥–æ–º –∏ –ø–æ –æ—á–µ—Ä–µ–¥–∏ –∏—Å–ø–æ–ª–Ω—è–µ—Ç
 * –∑–∞–¥–∞–Ω–Ω—ã–µ –µ–º—É –¥–µ–π—Å—Ç–≤–∏—è.
 * 
 * –ò—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–µ:
 * 1. –î–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è —ç–∫–∑–µ–º–ø–ª—è—Ä–∞ –∫–ª–∞—Å—Å–∞ –≤—ã –º–æ–∂–µ—Ç–µ
 *    –≤—ã–∑–≤–∞—Ç—å –∫–æ–Ω—Å—Ç—Ä—É–∫—Ç–æ—Ä –∏–ª–∏ –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å
 *    —Å—Ç–∞—Ç–∏—á–µ—Å–∫–∏–π –º–µ—Ç–æ–¥: `Tween.get()`
 * 2. –ü–æ—Å–ª–µ –ø–æ–ª—É—á–µ–Ω–∏—è —ç–∫–∑–µ–º–ø–ª—è—Ä–∞ –¥–æ–±–∞–≤—å—Ç–µ –≤ –Ω–µ–≥–æ
 *    –¥–µ–π—Å—Ç–≤–∏—è: `to()`, `wait()`, `call()`
 *    –∏ —Ç.–ø. –î–µ–π—Å—Ç–≤–∏—è –±—É–¥—É—Ç –≤—ã–ø–æ–ª–Ω—è—Ç—å—Å—è –ø–æ –æ—á–µ—Ä–µ–¥–∏.
 *    –î–æ–±–∞–≤–ª–µ–Ω–Ω—ã–µ –¥–µ–π—Å—Ç–≤–∏—è –Ω–µ–ª—å–∑—è —É–¥–∞–ª–∏—Ç—å.
 * 3. –ù–æ–≤—ã–π —Ç–≤–∏–Ω–µ—Ä –Ω–∞—Ö–æ–¥–∏—Ç—Å—è –≤ —Ä–µ–∂–∏–º–µ –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è,
 *    –≤—ã–∑–æ–≤: `play()` –Ω–µ —Ç—Ä–µ–±—É–µ—Ç—Å—è.
 * 4. –í—ã –º–æ–∂–µ—Ç–µ —É–ø—Ä–∞–≤–ª—è—Ç—å —Ö–æ–¥–æ–º –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è –∞–Ω–∏–º–∞—Ü–∏–∏
 *    —á–µ—Ä–µ–∑ —ç–∫–∑–µ–º–ø–ª—è—Ä –∫–ª–∞—Å—Å–∞ `Tween` –∏–ª–∏ –∑–∞–±–∏—Ç—å –Ω–∞
 *    –Ω–µ–≥–æ. (–û–Ω –±—É–¥–µ—Ç —É–¥–∞–ª—ë–Ω —Å–±–æ—Ä—â–∏–∫–æ–º –º—É—Å–æ—Ä–∞ –ø–æ—Å–ª–µ
 *    –∑–∞–≤–µ—Ä—à–µ–Ω–∏—è –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è)
 * 
 * –°–±–æ—Ä–∫–∞ –º—É—Å–æ—Ä–∞:
 * - –í—Å–µ –∞–∫—Ç–∏–≤–Ω—ã–µ —Ç–≤–∏–Ω–µ—Ä—ã –∏–º–µ—é—Ç —Å—Å—ã–ª–∫—É –∏ –Ω–µ –º–æ–≥—É—Ç
 *   –±—ã—Ç—å —É–¥–∞–ª–µ–Ω—ã.
 * - –ù–µ –∞–∫—Ç–∏–≤–Ω—ã–µ —Ç–≤–∏–Ω–µ—Ä—ã –∏ —Ç–≤–∏–Ω–µ—Ä—ã –Ω–∞ –ø–∞—É–∑–µ —É–¥–∞–ª—è—é—Ç—Å—è
 *   –∏–∑ –æ–±—â–µ–≥–æ —Ä–µ–µ—Å—Ç—Ä–∞ –∏ –º–æ–≥—É—Ç –±—ã—Ç—å —Å–æ–±—Ä–∞–Ω—ã —Å–±–æ—Ä—â–∏–∫–æ–º,
 *   –µ—Å–ª–∏ –Ω–∞ –Ω–∏—Ö –Ω–µ—Ç –¥—Ä—É–≥–∏—Ö —Å—Å—ã–ª–æ–∫.
 */
class Tween
{
    /**
     * –ú–∞–∫—Å–∏–º–∞–ª—å–Ω–æ–µ –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –æ–±—Ä–∞–±–∞—Ç—ã–≤–∞–µ–º—ã—Ö –¥–µ–π—Å—Ç–≤–∏–π
     * –∑–∞ –æ–¥–Ω–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ.
     */
    static private inline var MAX_CALLS = 10;

    private var a:Array<Action> = [];   // –ü–æ—Ä—è–¥–æ–∫ –∑–∞–¥–∞—á –¥–ª—è –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è.
    private var ai:Int = -1;            // –ò–Ω–¥–µ–∫—Å —Ç–µ–∫—É—â–µ–≥–æ –¥–µ–π—Å—Ç–≤–∏—è.
    private var ap:Float = 0;           // –ü—Ä–æ–≥—Ä–µ—Å—Å –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è —Ç–µ–∫—É—â–µ–≥–æ –¥–µ–π—Å—Ç–≤–∏—è. (mc –∏–ª–∏ scale) –ú–∏–ª–ª–∏—Å–µ–∫—É–Ω–¥—ã, –µ—Å–ª–∏ –∑–∞–¥–∞—á–∞ –∏–º–µ–µ—Ç duration>0, 0 –∏–ª–∏ 1 –≤ –¥—Ä—É–≥–∏—Ö —Å–ª—É—á–∞—è—Ö.
    private var si:Int = -1;            // –ò–Ω–¥–µ–∫—Å –ø–æ–ª–æ–∂–µ–Ω–∏—è –≤ –æ–±—â–µ–º –∫–µ—à–µ –¥–ª—è –±—ã—Å—Ç—Ä–æ–≥–æ –ø–æ–∏—Å–∫–∞.
    private var stp:Int = 0;            // –°—á—ë—Ç—á–∏–∫ –≤—ã–∑–æ–≤–æ–≤ step() –¥–ª—è –ø—Ä–µ–¥–æ—Ç–≤—Ä–∞—â–µ–Ω–∏—è —Ä–∞–Ω–Ω–µ–≥–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è.
    private var upd:Int = 0;            // –°—á—ë—Ç—á–∏–∫ –≤—ã–∑–æ–≤–æ–≤ update() –¥–ª—è –∏—Å–∫–ª—é—á–µ–Ω–∏—è –≤—ã–∑–æ–≤–∞ –≤ –≤—ã–∑–æ–≤–µ.
    private var opt:TweenOptions;

    /**
     * –°–æ–∑–¥–∞—Ç—å –Ω–æ–≤—ã–π —ç–∫–∑–µ–º–ø–ª—è—Ä.
     * @param target –£–ø—Ä–∞–≤–ª—è–µ–º—ã–π –æ–±—ä–µ–∫—Ç.
     * @param options –î–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω—ã–µ –ø–∞—Ä–∞–º–µ—Ç—Ä—ã.
     * @throws Error –¶–µ–ª—å —Ç–≤–∏–Ω–∞ –Ω–µ –¥–æ–ª–∂–Ω–∞ –±—ã—Ç—å: `null`
     */
    public function new(target:Dynamic, options:TweenOptions = null) {
        if (target == null)
            throw new Error("Tween target cannot be null");

        this.target = target;
        this.opt = options;

        if (target.tweenTargetID == null)
            target.tweenTargetID = ++targetID;

        if (options == null) {
            addTween(this);
        }
        else {
            if (options.bounce)
                bounce = true;
            if (options.reversed)
                reversed = true;
            if (options.loop != null)
                loop = options.loop;
            if (options.speed != null)
                speed = options.speed;
            if (options.clear)
                Tween.stop(target);
            if (!options.stopped)
                addTween(this);
        }
    }



    //////////////////
    //   –°–í–û–ô–°–¢–í–ê   //
    //////////////////

    /**
     * –¶–µ–ª—å –∞–Ω–∏–º–∞—Ü–∏–∏.  
     * –≠—Ç–æ —Å–≤–æ–π—Å—Ç–≤–æ —Å—Å—ã–ª–∞–µ—Ç—Å—è –Ω–∞ –æ–±—ä–µ–∫—Ç, —Å–≤–æ–π—Å—Ç–≤–∞
     * –∫–æ—Ç–æ—Ä–æ–≥–æ –±—É–¥—É—Ç –∞–Ω–∏–º–∏—Ä–æ–≤–∞–Ω—ã.
     * 
     * –ù–µ –º–æ–∂–µ—Ç –±—ã—Ç—å: `null`
     */
    public var target(default, null):Dynamic;

    /**
     * –°—á—ë—Ç—á–∏–∫ –∫–æ–ª–∏—á–µ—Å—Ç–≤–∞ –ø–æ–≤—Ç–æ—Ä–Ω—ã—Ö —Ü–∏–∫–ª–æ–≤
     * –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è.
     * - –û–¥–∏–Ω –ø—Ä–æ—Ö–æ–¥ –≤ –ª—é–±—É—é —Å—Ç–æ—Ä–æ–Ω—É —Å—á–∏—Ç–∞–µ—Ç—Å—è –∑–∞
     *   –æ–¥–∏–Ω —Ü–∏–∫–ª.
     * - –ê–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫–∏ —É–º–µ–Ω—å—à–∞–µ—Ç—Å—è –Ω–∞ `1` –ø—Ä–∏ –∑–∞–ø—É—Å–∫–µ
     *   –æ—á–µ—Ä–µ–¥–Ω–æ–≥–æ —Ü–∏–∫–ª–∞. (–ü–æ–∫–∞ –Ω–µ –¥–æ–π–¥—ë—Ç –¥–æ `0`)
     * - –ó–Ω–∞—á–µ–Ω–∏–µ `-1` (–∏–ª–∏ –º–µ–Ω—å—à–µ) –ø—Ä–∏–≤–æ–¥–∏—Ç –∫
     *   –±–µ—Å–∫–æ–Ω–µ—á–Ω–æ–º—É –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—é.
     * 
     * –ï—Å–ª–∏ –≤—ã –∏—Å–ø–æ–ª—å–∑—É–µ—Ç–µ: `bounce=true`, —Ç–æ –≤–∞–º
     * –Ω—É–∂–Ω–æ —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å —ç—Ç–æ –∑–Ω–∞—á–µ–Ω–∏–µ –≤: `1`, —á—Ç–æ–±—ã
     * —Ü–∏–∫–ª —Å –æ–±—Ä–∞—Ç–Ω–æ–π –∞–Ω–∏–º–∞—Ü–∏–µ–π —Ç–æ–∂–µ –ø—Ä–æ–∏–≥—Ä–∞–ª—Å—è.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `0` *(–û–¥–Ω–æ –ø—Ä–æ–∏–≥—Ä—ã–≤–∞–Ω–∏–µ)*
     */
    public var loop:Int = 0;

    /**
     * –û–±—Ä–∞—Ç–Ω–æ–µ –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏–µ –≤ –∫–æ–Ω—Ü–µ –∫–∞–∂–¥–æ–≥–æ —Ü–∏–∫–ª–∞.  
     * –ú–µ–Ω—è–µ—Ç –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–µ –¥–≤–∏–∂–µ–Ω–∏—è –Ω–∞ –ø—Ä–æ—Ç–∏–≤–æ–ø–æ–ª–æ–∂–Ω–æ–µ
     * –≤ –∫–æ–Ω—Ü–µ –∫–∞–∂–¥–æ–≥–æ —Ü–∏–∫–ª–∞ –∞–Ω–∏–º–∞—Ü–∏–∏. –ü—Ä–æ—Ö–æ–¥ –∞–Ω–∏–º–∞—Ü–∏–∏
     * –≤ –æ–¥–Ω–æ–º –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–∏ —Å—á–∏—Ç–∞–µ—Ç—Å—è –∑–∞ –æ–¥–∏–Ω —Ü–∏–∫–ª,
     * –ø–æ—ç—Ç–æ–º—É, –µ—Å–ª–∏ –≤—ã –∏—Å–ø–æ–ª—å–∑—É–µ—Ç–µ —ç—Ç–æ —Å–≤–æ–π—Å—Ç–≤–æ, –≤—ã
     * –¥–æ–ª–∂–Ω—ã —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –∑–Ω–∞—á–µ–Ω–∏–µ: `loop=1`, —á—Ç–æ–±—ã
     * —É–≤–∏–¥–µ—Ç—å –æ–±—Ä–∞—Ç–Ω—É—é –∞–Ω–∏–º–∞—Ü–∏—é.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false`
     */
    public var bounce:Bool = false;

    /**
     * –í–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏–µ –∞–Ω–∏–º–∞—Ü–∏–∏ –≤ –æ–±—Ä–∞—Ç–Ω–æ–º –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–∏.  
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false`
     */
    public var reversed:Bool = false;

    /**
     * –¢–≤–∏–Ω –æ—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω.
     * - –ï—Å–ª–∏: `true`, —ç—Ç–æ—Ç —Ç–≤–∏–Ω –Ω–µ –≤–æ—Å–ø—Ä–æ–∏–∑–≤–æ–¥–∏—Ç—Å—è –∏
     *   –º–æ–∂–µ—Ç –±—ã—Ç—å —É–¥–∞–ª—ë–Ω —Å–±–æ—Ä—â–∏–∫–æ–º –º—É—Å–æ—Ä–∞, –µ—Å–ª–∏ –Ω–∞
     *   –Ω–µ–≥–æ –Ω–µ—Ç –¥—Ä—É–≥–∏—Ö —Å—Å—ã–ª–æ–∫. –û—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–Ω—ã–µ —Ç–≤–∏–Ω—ã –Ω–µ
     *   –≤–æ—Å–ø—Ä–æ–∏–∑–≤–æ–¥—è—Ç—Å—è, –µ—Å–ª–∏ –≤ –Ω–∏—Ö –¥–æ–±–∞–≤–ª—è—é—Ç—Å—è –Ω–æ–≤—ã–µ
     *   –¥–µ–π—Å—Ç–≤–∏—è. –î–ª—è –∑–∞–ø—É—Å–∫–∞ —Ç–≤–∏–Ω–∞ –≤—ã –¥–æ–ª–∂–Ω—ã –≤—ã–∑–≤–∞—Ç—å
     *   –º–µ—Ç–æ–¥: `play()`, –∏–ª–∏: `goto()`. –í—Å–µ —Ç–≤–∏–Ω—ã
     *   –∞–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫–∏ –æ—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞—é—Ç—Å—è, –∫–æ–≥–¥–∞ –∑–∞–≤–µ—Ä—à–∞–µ—Ç—Å—è
     *   –∏—Ö –∞–Ω–∏–º–∞—Ü–∏—è –∏–ª–∏ –µ—Å–ª–∏ –±—ã–ª–∏ –≤—ã–∑–≤–∞–Ω—ã –º–µ—Ç–æ–¥—ã:
     *   `pause()`, `stop()`, `stopAll()`
     * - –ï—Å–ª–∏: `false`, —ç—Ç–æ—Ç —Ç–≤–∏–Ω —Å—á–∏—Ç–∞–µ—Ç—Å—è –∞–∫—Ç–∏–≤–Ω—ã–º –∏ –Ω–∞
     *   –Ω–µ–≥–æ –µ—Å—Ç—å —Å—Å—ã–ª–∫–∞ –≤ –æ–±—â–µ–º —Ä–µ–µ—Å—Ç—Ä–µ –≤—Å–µ—Ö —Ç–≤–∏–Ω–æ–≤.
     *   –û–Ω –∞–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫–∏ –æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—Å—è, –∫–æ–≥–¥–∞ –≤—Å—è –∞–Ω–∏–º–∞—Ü–∏—è
     *   –±—É–¥–µ—Ç –ø—Ä–æ–∏–≥—Ä–∞–Ω–∞. (–ï—Å–ª–∏ –Ω–µ –∑–∞–¥–∞–Ω –±–µ—Å–∫–æ–Ω–µ—á–Ω—ã–π —Ü–∏–∫–ª)
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false` *(–ü—Ä–æ–∏–≥—Ä—ã–≤–∞–µ—Ç—Å—è)*
     */
    public var stopped(get, never):Bool;
    inline function get_stopped():Bool {
        return NativeJS.eq(si, -1);
    }

    /**
     * –¢–µ–∫—É—â–∞—è –ø–æ–∑–∏—Ü–∏—è –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è.  
     * –°–æ–¥–µ—Ä–∂–∏—Ç –Ω–æ—Ä–º–∞–ª–∏–∑–æ–≤–∞–Ω–Ω—É—é –ø–æ–∑–∏—Ü–∏—é –∞–Ω–∏–º–∞—Ü–∏–∏, –≥–¥–µ:
     * - `0` - –ù–∞—á–∞–ª–æ –∞–Ω–∏–º–∞—Ü–∏–∏.
     * - `1` - –ö–æ–Ω–µ—Ü –∞–Ω–∏–º–∞—Ü–∏–∏, –ø–æ –≤—Ä–µ–º–µ–Ω–∏ —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–∏–π: `duration`
     * 
     * –≠—Ç–æ –∑–Ω–∞—á–µ–Ω–∏–µ –Ω–µ –º–æ–∂–µ—Ç –±—ã—Ç—å –º–µ–Ω—å—à–µ `0` –∏–ª–∏ –±–æ–ª—å—à–µ `1`
     * –î–ª—è –ø–æ–ª—É—á–µ–Ω–∏—è –ø–æ–∑–∏—Ü–∏–∏ –≤ –º–∏–ª–ª–∏—Å–µ–∫—É–Ω–¥–∞—Ö –∏—Å–ø–æ–ª—å–∑—É–π—Ç–µ: `position*duration`
     * 
     * –û–±—Ä–∞—Ç–∏—Ç–µ –≤–Ω–∏–º–∞–Ω–∏–µ, —á—Ç–æ –ø—Ä–∏ —É—Å—Ç–∞–Ω–æ–≤–∫–µ —ç—Ç–æ–≥–æ –∑–Ω–∞—á–µ–Ω–∏—è –≤—Ä—É—á–Ω—É—é –±—É–¥—É—Ç
     * –ø—Ä–æ–ø—É—â–µ–Ω—ã –¥–µ–π—Å—Ç–≤–∏—è –º–µ–∂–¥—É –∏—Å—Ö–æ–¥–Ω–æ–π –∏ –Ω–æ–≤–æ–π –ø–æ–∑–∏—Ü–∏—è–º–∏. –≠—Ç–æ –∫–∞—Å–∞–µ—Ç—Å—è,
     * –≤ –ø–µ—Ä–≤—É—é –æ—á–µ—Ä–µ–¥—å, –≤—ã–∑—ã–≤–∞–µ–º—ã—Ö –∫–æ–ª–±–µ–∫–æ–≤: `call()` –∏ –∫–æ—Ä–æ—Ç–∫–∏—Ö –¥–µ–π—Å—Ç–≤–∏–π
     * —Å `duration=0`. –ï—Å–ª–∏ –≤–∞–º –Ω—É–∂–Ω–æ –≤—ã–ø–æ–ª–Ω–∏—Ç—å –≤—Å–µ –¥–µ–π—Å—Ç–≤–∏—è –º–µ–∂–¥—É –ø–æ–∑–∏—Ü–∏—è–º–∏,
     * –∏—Å–ø–æ–ª—å–∑—É–π—Ç–µ –º–µ—Ç–æ–¥: `update()`. –î–æ–±–∞–≤–ª–µ–Ω–∏–µ –≤ —Ç–≤–∏–Ω –Ω–æ–≤—ã—Ö –¥–µ–π—Å—Ç–≤–∏–π —Å–º–µ—â–∞–µ—Ç
     * position –≤–ª–µ–≤–æ.
     * 
     * *–î–µ—Ç–∞–ª–∏ —Ä–µ–∞–ª–∏–∑–∞—Ü–∏–∏:*
     * *–°–≤–æ–π—Å—Ç–≤–æ position —è–≤–ª—è–µ—Ç—Å—è –æ—Ç—Ä–∞–∂–µ–Ω–∏–µ–º –æ–±—â–µ–≥–æ –ø—Ä–æ–≥—Ä–µ—Å—Å–∞ –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è —Ç–≤–∏–Ω–∞*
     * *–¥–ª—è –±–æ–ª–µ–µ —É–ø—Ä–æ—â—ë–Ω–Ω–æ–≥–æ –≤–Ω–µ—à–Ω–µ–≥–æ API. –≠—Ç–æ—Ç –ø—Ä–æ–≥—Ä–µ—Å—Å, —Ñ–∞–∫—Ç–∏—á–µ—Å–∫–∏, —Å–æ–¥–µ—Ä–∂–∏—Ç—Å—è*
     * *–≤ –ø—Ä–∏–≤–∞—Ç–Ω—ã—Ö –ø–µ—Ä–µ–º–µ–Ω–Ω—ã—Ö: ai –∏ ap. (–ò–Ω–¥–µ–∫—Å –¥–µ–π—Å—Ç–≤–∏—è, –ø—Ä–æ–≥—Ä–µ—Å—Å –¥–µ–π—Å—Ç–≤–∏—è)*
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `0`
     */
    public var position(get, set):Float;
    function get_position():Float {
        var len = a.length;
        if (NativeJS.eq(len, 0))
            return ap; // <-- –í –ø—É—Å—Ç–æ–º —Å–ø–∏—Å–∫–µ –º–æ–∂–µ—Ç –±—ã—Ç—å —Ç–æ–ª—å–∫–æ 0 –∏–ª–∏ 1.
        if (NativeJS.eq(duration, 0))
            return (ai+(ap==0?0:1))/len; // <-- –° –Ω—É–ª–µ–≤–æ–π –¥–ª–∏—Ç–µ–ª—å–Ω–æ—Å—Ç—å—é ap –º–æ–∂–µ—Ç –±—ã—Ç—å —Ç–æ–ª—å–∫–æ 0 –∏–ª–∏ 1.

        return (a[ai].prev+ap)/duration; // <-- –¢—É—Ç –∑–∞–¥–∞—á–∞ —Å –≤—Ä–µ–º–µ–Ω–µ–º, ap –∏–∑–º–µ—Ä—è–µ—Ç—Å—è –≤ –º–∏–ª–ª–∏—Å–µ–∫—É–Ω–¥–∞—Ö –ø—Ä–æ–≥—Ä–µ—Å—Å–∞.
    }
    function set_position(value:Float):Float {
        var v:Float = 0;
        if (value > 0)
            v = value;
        if (v > 1)
            v = 1;

        var len = a.length;
        if (len == 0) {
            ap = value<0.5?0:1;
        }
        else if (NativeJS.eq(duration, 0)) {
            ai = untyped Math.min(len-1, Math.floor(v*len));
            ap = (v*len)-ai<0.5?0:1;
        }
        else {
            if (v == 0) {
                ai = 0;
                ap = 0;
            }
            else if (v == 1) {
                ai = len-1;
                ap = 1;
            }
            else {
                var t = v * duration;
                while (len-- != 0) {
                    if (t < a[len].prev)
                        continue;
                    ai = len;
                    ap = (t-a[len].prev)/a[len].duration;
                    return value;
                }
            }
        } 

        return value;
    }

    /**
     * –û–±—â–∞—è –ø—Ä–æ–¥–æ–ª–∂–∏—Ç–µ–ª—å–Ω–æ—Å—Ç—å –≤—Å–µ—Ö –∞–Ω–∏–º–∞—Ü–∏–∏. *(mc)*  
     * –≠—Ç–æ –∑–Ω–∞—á–µ–Ω–∏–µ —Å–æ–¥–µ—Ä–∂–∏—Ç –æ–±—â–µ–µ –≤—Ä–µ–º—è –≤—Å–µ—Ö –∞–Ω–∏–º–∞—Ü–∏–π —Ç–≤–∏–Ω–∞ –±–µ–∑ —É—á—ë—Ç–∞
     * —Ü–∏–∫–ª–æ–≤. –û–±–Ω–æ–≤–ª—è–µ—Ç—Å—è –∞–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫–∏ –ø—Ä–∏ –¥–æ–±–∞–≤–ª–µ–Ω–∏–∏ –Ω–æ–≤—ã—Ö –¥–µ–π—Å—Ç–≤–∏–π.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `0`
     */
    public var duration(default, null):Float = 0;

    /**
     * –°–∫–æ—Ä–æ—Å—Ç—å –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è. *(scale)*  
     * - –≠—Ç–æ –∑–Ω–∞—á–µ–Ω–∏–µ –Ω–µ –º–æ–∂–µ—Ç –±—ã—Ç—å –º–µ–Ω—å—à–µ: `0`
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `1` *(–û–±—ã—á–Ω–∞—è —Å–∫–æ—Ä–æ—Å—Ç—å)*
     */
    public var speed(default, set):Float = 1;
    function set_speed(value:Float):Float {
        if (value > 0)
            speed = value;
        else
            speed = 0;

        return value;
    }



    ////////////////
    //   –ú–ï–¢–û–î–´   //
    ////////////////

    /**
     * –û—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –∞–Ω–∏–º–∞—Ü–∏—é.  
     * –í—ã–∑–æ–≤ –∏–≥–Ω–æ—Ä–∏—Ä—É–µ—Ç—Å—è, –µ—Å–ª–∏ —Ç–≤–∏–Ω–µ—Ä —É–∂–µ –æ—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω.
     * –û—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–Ω—ã–µ —Ç–≤–∏–Ω–µ—Ä—ã –Ω–µ –∏–º–µ—é—Ç —Å—Å—ã–ª–∫–∏ –∏ –º–æ–≥—É—Ç
     * –±—ã—Ç—å —É–¥–∞–ª–µ–Ω—ã —Å–±–æ—Ä—â–∏–∫–æ–º –º—É—Å–æ—Ä–∞. (–ï—Å–ª–∏ –¥—Ä—É–≥–∏—Ö
     * —Å—Å—ã–ª–æ–∫ –Ω–µ—Ç)
     */
    public function pause():Void {
        if (stopped)
            return;

        removeTween(this);
    }

    /**
     * –ü—Ä–æ–¥–æ–ª–∂–∏—Ç—å –∞–Ω–∏–º–∞—Ü–∏—é.
     */
    public function play():Void {
        if (!stopped)
            return;

        addTween(this);
    }

    /**
     * –ü—Ä–æ–¥–≤–∏–Ω—É—Ç—å –∞–Ω–∏–º–∞—Ü–∏—é —Ç–≤–∏–Ω–∞.
     * - –ü–æ–¥–¥–µ—Ä–∂–∏–≤–∞—é—Ç—Å—è –æ—Ç—Ä–∏—Ü–∞—Ç–µ–ª—å–Ω—ã–µ –∑–Ω–∞—á–µ–Ω–∏—è
     *   –¥–ª—è –∏–Ω–≤–µ—Ä—Å–∏–∏ –∞–Ω–∏–º–∞—Ü–∏–∏.
     * @param time –ü—Ä–æ—à–µ–¥—à–µ–µ –≤—Ä–µ–º—è. *(mc)*
     */
    public function update(time:Float):Void {
        var age = ++upd;
        if (age == 1 && opt != null && opt.position != null) {
            var rev = reversed;
            reversed = false;
            update(opt.position * duration);
            reversed = rev;
            if (stopped || NativeJS.neq(age+1,upd))
                return;
            age = upd;
        }

        if (reversed)
            time = time * speed * -1;
        else
            time = time * speed;

        // –í—Ä–µ–º—è –Ω–µ –ø–µ—Ä–µ–¥–∞–Ω–æ: (–ï—Å–ª–∏ –Ω–µ—Ç –≤—Ä–µ–º–µ–Ω–∏ - –Ω–∏—á–µ–≥–æ –Ω–µ –º–æ–∂–µ—Ç –ø—Ä–æ–∏–∑–æ–π—Ç–∏, –¥–∞–∂–µ –º–≥–Ω–æ–≤–µ–Ω–∏–µ)
        if (NativeJS.isNaN(time) || NativeJS.eq(time, 0))
            return;

        // –ü—É—Å—Ç–æ–π —Ç–≤–∏–Ω: (–ó–∞–±—ã–ª–∏ –∑–∞–ø–æ–ª–Ω–∏—Ç—å?)
        var len = a.length;
        if (NativeJS.eq(len, 0)) {
            if (bounce)
                reversed = !reversed;

            if (time > 0)
                ap = 1;
            else
                ap = 0;
            
            if (loop > 0)
                loop--;
            else if (NativeJS.eq(loop,0) && !stopped)
                removeTween(this);

            return;
        }

        // –ü–æ –æ—á–µ—Ä–µ–¥–∏ –≤—ã–ø–æ–ª–Ω—è–µ–º –≤—Å–µ –∑–∞–¥–∞—á–∏.
        //
        // –í–æ –≤—Ä–µ–º—è –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—å –º–æ–∂–µ—Ç –ø–æ–≤–ª–∏—è—Ç—å –Ω–∞ —Ç–≤–∏–Ω–µ—Ä, –ø–æ—ç—Ç–æ–º—É
        // –Ω–∞ –∫–∞–∂–¥–æ–º —à–∞–≥–µ –µ—Å—Ç—å –¥–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω—ã–µ —É—Å–ª–æ–≤–∏—è:
        //   1. –ï—Å–ª–∏ —Ç–≤–∏–Ω –±—ã–ª –ø–æ—Å—Ç–∞–≤–ª–µ–Ω –Ω–∞ –ø–∞—É–∑—É, –≤—ã–ø–æ–ª–Ω–µ–Ω–∏–µ –ø—Ä–µ–∫—Ä–∞—â–∞–µ—Ç—Å—è.
        //   2. –ï—Å–ª–∏ –±—ã–ª –≤—ã–∑–≤–∞–Ω –º–µ—Ç–æ–¥ update() –ø–æ–≤—Ç–æ—Ä–Ω–æ, –≤—ã–ø–æ–ª–Ω–µ–Ω–∏–µ –ø—Ä–µ–∫—Ä–∞—â–∞–µ—Ç—Å—è.
        //   3. –ï—Å–ª–∏ –±—ã–ª –¥–æ—Å—Ç–∏–≥–Ω—É—Ç –ø—Ä–µ–¥–µ–ª –¥–µ–π—Å—Ç–≤–∏–π –¥–ª—è –æ–¥–Ω–æ–≥–æ –≤—ã–∑–æ–≤–∞ MAX_CALL, –≤—ã–ø–æ–ª–Ω–µ–Ω–∏–µ –ø—Ä–µ–∫—Ä–∞—â–∞–µ—Ç—Å—è.

        var limit = MAX_CALLS;
        while (limit-- != 0) {
            var act = a[ai];
            var p = ap + time;

            // –ü—Ä–æ–≥—Ä–µ—Å—Å –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è:
            if (p < 0) {
                // –ó–∞–¥–∞—á–∞ –≤—ã–ø–æ–ª–Ω–µ–Ω–∞. (–†–µ–≤–µ—Ä—Å)
                time += ap;
                ap = 0;
                executeAction(act, 0);

                // –í–Ω–µ—à–Ω—è—è –æ—Ç–º–µ–Ω–∞:
                if (stopped || NativeJS.neq(age,upd))
                    return;

                // –î–æ—Å—Ç–∏–≥–Ω—É—Ç–æ –Ω–∞—á–∞–ª–æ —Å–ø–∏—Å–∫–∞:
                if (NativeJS.eq(--ai,-1)) {
                    if (bounce) {
                        reversed = !reversed;
                        time *= -1;
                    }
                    if (NativeJS.eq(loop,0)) {
                        ai ++;
                        removeTween(this);
                        return;
                    }
                    if (loop > 0)
                        loop --;
                    ai ++;
                }
                else {
                    ap = NativeJS.eq(a[ai].duration,0)?1:a[ai].duration;
                }
            }
            else if (p >= act.duration && time > 0) {
                // –ó–∞–¥–∞—á–∞ –≤—ã–ø–æ–ª–Ω–µ–Ω–∞: (–ù–æ—Ä–º–∞)
                if (NativeJS.eq(act.duration, 0)) {
                    ap = 1;
                }
                else {
                    time -= (act.duration - ap);
                    ap = act.duration;
                }
                executeAction(act, 1);

                // –í–Ω–µ—à–Ω—è—è –æ—Ç–º–µ–Ω–∞:
                if (stopped || NativeJS.neq(age,upd))
                    return;

                // –î–æ—Å—Ç–∏–≥–Ω—É—Ç –∫–æ–Ω–µ—Ü —Å–ø–∏—Å–∫–∞:
                if (NativeJS.eq(++ai,len)) {
                    if (NativeJS.eq(loop,0)) {
                        if (bounce) {
                            reversed = !reversed;
                            time *= -1;
                        }
                        ai --;
                        removeTween(this);
                        return;
                    }
                    if (loop > 0)
                        loop --;
                    if (bounce) {
                        reversed = !reversed;
                        time *= -1;
                        ai --;
                    }
                    else {
                        ai = 0;
                        ap = 0;
                    }
                }
                else {
                    ap = 0;
                }
            }
            else {
                // –ó–∞–¥–∞—á–∞ –≤—ã–ø–æ–ª–Ω—è–µ—Ç—Å—è:
                ap = p;
                executeAction(act, NativeJS.eq(act.duration,0)?0:(p/act.duration));
                return;
            }
        }

        #if debug
        // –°–ª–∏—à–∫–æ–º –º–Ω–æ–≥–æ –≤—ã–∑–æ–≤–æ–≤:
        Browser.console.warn('Tween execute actions calls limit reached: ', this);
        #end
    }

    private function executeAction(action:Action, progress:Float):Void {
        if (NativeJS.eq(action.type, ActionType.EASE)) {
            var prop:Dynamic = null;
            Syntax.code('for ({0} in {1}) {', prop, action.props); // for in
                var v1:Dynamic = action.cache[prop]; // –ò—Å—Ö–æ–¥–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ
                var v2:Dynamic = action.props[prop]; // –¶–µ–ª–µ–≤–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ

                // –ß–∏—Å–ª–æ:
                if (NativeJS.isNum(v2)) {
                    if (NativeJS.isUnd(v1)) {
                        v1 = NativeJS.parseFloat(getTargetProperty(prop)); // –ó–∞–ø–æ–º–∏–Ω–∞–µ–º –∏—Å—Ö–æ–¥–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ
                        if (!NativeJS.isFin(v1))
                            v1 = 0;
                        action.cache[prop] = v1;
                    }
                    setTargetPropert(prop, v1+action.ease(progress)*(v2-v1));
                    Syntax.code('continue');
                }

                // –ü–æ–ø—ã—Ç–∫–∞ —Ä–∞–∑–±–æ—Ä–∞ –≤ —á–∏—Å–ª–æ:
                if (opt != null && opt.parse) {
                    var v2p:Dynamic = NativeJS.parseFloat(v2);
                    if (NativeJS.isFin(v2p)) {

                        // –ü–∞—Ä—Å–∏–Ω–≥ –≤ —á–∏—Å–ª–æ —É–¥–∞–ª—Å—è:
                        if (NativeJS.isUnd(v1)) {
                            v1 = NativeJS.parseFloat(getTargetProperty(prop)); // –ó–∞–ø–æ–º–∏–Ω–∞–µ–º –∏—Å—Ö–æ–¥–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ
                            if (!NativeJS.isFin(v1))
                                v1 = 0;
                            action.cache[prop] = v1;
                        }

                        if (NativeJS.isStr(v2))
                            v2p = NativeJS.str(v1+action.ease(progress)*(v2p-v1));
                        else
                            v2p = v1+action.ease(progress)*(v2p-v1);

                        setTargetPropert(prop, v2p);
                        Syntax.code('continue');
                    }
                }

                // –í—Å–µ –æ—Å—Ç–∞–ª—å–Ω—ã–µ —Ç–∏–ø—ã –¥–∞–Ω–Ω—ã—Ö:
                if (NativeJS.isUnd(v1)) {
                    v1 = getTargetProperty(prop); // –ó–∞–ø–æ–º–∏–Ω–∞–µ–º –∏—Å—Ö–æ–¥–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ
                    if (NativeJS.isUnd(v1))
                        v1 = null;
                    action.cache[prop] = v1;
                }
                setTargetPropert(prop, NativeJS.eq(progress,1)?v2:v1);
            Syntax.code('}'); // for end
            return;
        }
        if (NativeJS.eq(action.type, ActionType.CALL)) {
            if (NativeJS.eq(progress,1) && action.callback != null)
                Syntax.code('{0}.apply(null, {1})', action.callback, action.args);
            return;
        }
    }

    private function getTargetProperty(prop:String):Dynamic {
        if (prop == null)
            return null;

        if (opt != null && opt.multilevel && NativeJS.isStr(prop)) {
            var arr:Array<String> = prop.split(".");
            var i = 0;
            var t = target;
            while (i < arr.length-1) {
                t = Reflect.getProperty(t, arr[i++]);
                if (t == null)
                    return null;
            }
            return Reflect.getProperty(t, arr[i]);
        }

        return Reflect.getProperty(target, prop);
    }

    private function setTargetPropert(prop:String, value:Dynamic):Void {
        if (prop == null)
            return;

        if (opt == null) {
            Reflect.setProperty(target, prop, value);
            return;
        }

        var t = target;
        if (opt.multilevel && NativeJS.isStr(prop)) {
            var arr:Array<String> = prop.split(".");
            var i = 0;
            while (i < arr.length-1) {
                prop = arr[i++];
                t = Reflect.getProperty(t, prop);
                if (t == null)
                    return;
            }
            prop = arr[i];
        }

        if (opt.modifier == null)
            Reflect.setProperty(t, prop, value);
        else
            Reflect.setProperty(t, prop, opt.modifier(value));
    }

    private function addAction(action:Action):Tween {
        if (NativeJS.eq(ai, -1)) {
            a[0] = action;
            ai = 0;
            ap = 0;
            action.prev = 0;
            duration = action.duration;
        }
        else {
            a.push(action);
            action.prev = duration;
            duration += action.duration;
        }
        return this;
    }



    //////////////////
    //   –î–ï–ô–°–¢–í–ò–Ø   //
    //////////////////

    /**
     * –î–æ–±–∞–≤–∏—Ç—å –Ω–æ–≤—É—é –∞–Ω–∏–º–∞—Ü–∏—é.
     * - –ù—É–ª–µ–≤–∞—è –ø—Ä–æ–¥–æ–ª–∂–∏—Ç–µ–ª—å–Ω–æ—Å—Ç—å –∞–Ω–∏–º–∞—Ü–∏–∏ –ø—Ä–∏–≤–µ–¥—ë—Ç
     *   –∫ –º–≥–Ω–æ–≤–µ–Ω–Ω–æ–π —É—Å—Ç–∞–Ω–æ–≤–∫–µ —Ü–µ–ª–µ–≤—ã—Ö –∑–Ω–∞—á–µ–Ω–∏–π.
     * - –ß–∏—Å–ª–æ–≤—ã–µ —Å–≤–æ–π—Å—Ç–≤–∞ –±—É–¥—É—Ç –∞–Ω–∏–º–∏—Ä–æ–≤–∞–Ω—ã –æ—Ç –∏—Ö
     *   —Ç–µ–∫—É—â–∏—Ö –∑–Ω–∞—á–µ–Ω–∏–π –Ω–∞ –º–æ–º–µ–Ω—Ç –∑–∞–ø—É—Å–∫–∞.
     * - –ù–µ—á–∏—Å–ª–æ–≤—ã–µ —Å–≤–æ–π—Å—Ç–≤–∞ –±—É–¥—É—Ç —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω—ã –≤ –∫–æ–Ω—Ü–µ
     *   —É–∫–∞–∑–∞–Ω–Ω–æ–π –ø—Ä–æ–¥–æ–ª–∂–∏—Ç–µ–ª—å–Ω–æ—Å—Ç–∏.
     * 
     * –ü—Ä–∏–º–µ—Ä:
     * ```
     * Tween.get(target).to({x:120, alpha:0, visible:false}, 1000, Ease.elasticInOut);
     * ```
     * @param params –¶–µ–ª–µ–≤—ã–µ –∑–Ω–∞—á–µ–Ω–∏—è —Å–≤–æ–π—Å—Ç–≤. –ü—Ä–∏–º–µ—Ä: `{ x:150, alpha:0, visible:false }`
     * @param duration –ü—Ä–æ–¥–æ–ª–∂–∏—Ç–µ–ª—å–Ω–æ—Å—Ç—å –∞–Ω–∏–º–∞—Ü–∏–∏ –≤ –º–∏–ª–ª–∏—Å–µ–∫—É–Ω–¥–∞—Ö.
     * @param ease –§—É–Ω–∫—Ü–∏—è –∞–Ω–∏–º–∞—Ü–∏–∏. –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `Ease.linear`
     * @return –≠—Ç–æ—Ç —ç–∫–∑–µ–º–ø–ª—è—Ä —Ç–≤–∏–Ω–∞ –¥–ª—è —É–¥–æ–±–Ω–æ–≥–æ –ø–æ—Å—Ç—Ä–æ–µ–Ω–∏—è —Ü–µ–ø–æ—á–∫–∏.
     */
    public function to(params:Dynamic, duration:Float = 0, ease:EaseFunction = null):Tween {
        if (!NativeJS.isNum(duration) || !NativeJS.isFin(duration) || duration < 0)
            return addAction({ type:ActionType.EASE, duration:0, props:params, ease:ease==null?Ease.linear:ease, cache:{} });

        return addAction({ type:ActionType.EASE, duration:duration, props:params, ease:ease==null?Ease.linear:ease, cache:{} });
    }

    /**
     * –î–æ–±–∞–≤–∏—Ç—å –æ–∂–∏–¥–∞–Ω–∏–µ. *(–ü—É—Å—Ç—É—é –∞–Ω–∏–º–∞—Ü–∏—é)*
     * @param duration –ü—Ä–æ–¥–æ–ª–∂–∏—Ç–µ–ª—å–Ω–æ—Å—Ç—å –æ–∂–∏–¥–∞–Ω–∏—è –≤ –º–∏–ª–ª–∏—Å–µ–∫—É–Ω–¥–∞—Ö.
     * @return –≠—Ç–æ—Ç —ç–∫–∑–µ–º–ø–ª—è—Ä —Ç–≤–∏–Ω–∞ –¥–ª—è —É–¥–æ–±–Ω–æ–≥–æ –ø–æ—Å—Ç—Ä–æ–µ–Ω–∏—è —Ü–µ–ø–æ—á–∫–∏.
     */
    public function wait(duration:Float):Tween {
        if (!NativeJS.isNum(duration) || !NativeJS.isFin(duration) || duration < 0)
            return addAction({ type:ActionType.WAIT, duration:0 });

        return addAction({ type:ActionType.WAIT, duration:duration });
    }

    /**
     * –î–æ–±–∞–≤–∏—Ç—å –≤—ã–∑–æ–≤ –ø—Ä–æ–∏–∑–≤–æ–ª—å–Ω–æ–π —Ñ—É–Ω–∫—Ü–∏–∏.
     * @param callback –í—ã–∑—ã–≤–∞–µ–º–∞—è —Ñ—É–Ω–∫—Ü–∏—è.
     * @param args –ü–µ—Ä–µ–¥–∞–≤–∞–µ–º—ã–µ –∞—Ä–≥—É–º–µ–Ω—Ç—ã –≤ —Ñ—É–Ω–∫—Ü–∏—é.
     * @return –≠—Ç–æ—Ç —ç–∫–∑–µ–º–ø–ª—è—Ä —Ç–≤–∏–Ω–∞ –¥–ª—è —É–¥–æ–±–Ω–æ–≥–æ –ø–æ—Å—Ç—Ä–æ–µ–Ω–∏—è —Ü–µ–ø–æ—á–∫–∏.
     */
    public function call(callback:Function, args:Array<Dynamic> = null):Tween {
        return addAction({ type:ActionType.CALL, duration:0, callback:callback, args:args });     
    }



    /////////////////
    //   –°–¢–ê–¢–ò–ö–ê   //
    /////////////////

    static private var all:Dynamic = {}; // targetID->[Tween, Tween, null, Tween, ...]
    static private var targetID:Int = 0;
    static private var stamp:Float = 0;
    static private var steps:Int = 0;
    static private var request:Bool = false;

    /**
     * –†—É—á–Ω–æ–π –≤—ã–∑–æ–≤ –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è —Ç–≤–∏–Ω–æ–≤.  
     * –≠—Ç–æ –∑–Ω–∞—á–µ–Ω–∏–µ –ø–æ–ª–µ–∑–Ω–æ, –µ—Å–ª–∏ –≤—ã –∏—Å–ø–æ–ª—å–∑—É–µ—Ç–µ
     * —Å–æ–±—Å—Ç–≤–µ–Ω–Ω—ã–π heartbeat –∏ —Ö–æ—Ç–∏—Ç–µ —Å–∞–º–æ—Å—Ç–æ—è—Ç–µ–ª—å–Ω–æ
     * –≤—ã–∑—ã–≤–∞—Ç—å –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ –≤—Å–µ—Ö —Ç–≤–∏–Ω–æ–≤.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false` *(–†—É—á–Ω–æ–π —Ä–µ–∂–∏–º –≤—ã–∫–ª—é—á–µ–Ω)*
     */
    static public var handRedrawMode(default, set):Bool = false;
    static function set_handRedrawMode(value:Bool):Bool {
        if (handRedrawMode == value)
            return value;

        handRedrawMode = value;
        if (!value && !request) {
            request = true;
            stamp = NativeJS.now();
            Browser.window.requestAnimationFrame(onUpdate);
        }

        return value;
    }

    static private function addTween(tween:Tween):Void {
        var arr:Array<Tween> = all[tween.target.tweenTargetID];
        if (arr == null) {
            arr = [tween];
            tween.si = 0;
            all[tween.target.tweenTargetID] = arr;
        }
        else {
            tween.si = arr.length;
            arr[tween.si] = tween;
        }

        // –ï—Å—Ç—å –≤–µ—Ä–æ—è—Ç–Ω–æ—Å—Ç—å, —á—Ç–æ –ø—Ä–∏ –¥–æ–±–∞–≤–ª–µ–Ω–∏–∏ —ç—Ç–æ–≥–æ —Ç–≤–∏–Ω–∞ —É–∂–µ
        // –≤—ã–ø–æ–ª–Ω—è–µ—Ç—Å—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ø—Ä—è–º–æ —Å–µ–π—á–∞—Å. –° –ø–æ–º–æ—â—å—é —ç—Ç–æ–≥–æ
        // –º—ã –≥–∞—Ä–∞–Ω—Ç–∏—Ä—É–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ —Ç–≤–∏–Ω–∞ –≤ —Å–ª–µ–¥—É—é—â–µ–º –≤—ã–∑–æ–≤–µ step().
        tween.stp = steps;

        if (!request && !handRedrawMode) {
            request = true;
            stamp = NativeJS.now();
            Browser.window.requestAnimationFrame(onUpdate);
        }
    }

    static private function removeTween(tween:Tween):Void {
        all[tween.target.tweenTargetID][tween.si] = null;
        tween.si = -1;
    }

    static private function onUpdate(v:Dynamic):Void {
        var now = NativeJS.now();
        var dt = now - stamp;
        stamp = now;
        request = false;

        if (!step(dt) && !handRedrawMode) {
            Browser.window.requestAnimationFrame(onUpdate);
            request = true;
        }
    }

    /**
     * –°–æ–∑–¥–∞—Ç—å –Ω–æ–≤—ã–π —Ç–≤–∏–Ω–µ—Ä.
     * @param target –ê–Ω–∏–º–∏—Ä—É–µ–º—ã–π –æ–±—ä–µ–∫—Ç.
     * @param params –ü–∞—Ä–∞–º–µ—Ç—Ä—ã –¥–ª—è –Ω–æ–≤–æ–≥–æ —Ç–≤–∏–Ω–µ—Ä–∞.
     * @return –ù–æ–≤—ã–π —Ç–≤–∏–Ω–µ—Ä.
     */
    static public function get(target:Dynamic, params:TweenOptions = null):Tween {
        return new Tween(target, params);
    }

    /**
     * –ü—Ä–æ–≤–µ—Ä–∏—Ç—å –Ω–∞–ª–∏—á–∏–µ –∞–∫—Ç–∏–≤–Ω—ã—Ö —Ç–≤–∏–Ω–æ–≤ –Ω–∞ —É–∫–∞–∑–∞–Ω–Ω–æ–º –æ–±—ä–µ–∫—Ç–µ.
     * - –í–æ–∑–≤—Ä–∞—â–∞–µ—Ç: `true`, –µ—Å–ª–∏ –æ–±—ä–µ–∫—Ç –∏–º–µ–µ—Ç —Ö–æ—Ç—è –±—ã –æ–¥–∏–Ω –∞–∫—Ç–∏–≤–Ω—ã–π —Ç–≤–∏–Ω –≤ –¥–∞–Ω–Ω—ã–π –º–æ–º–µ–Ω—Ç.
     * - –í–æ–∑–≤—Ä–∞—â–∞–µ—Ç: `false`, –µ—Å–ª–∏ –ø–µ—Ä–µ–¥–∞–Ω: `null` –∏–ª–∏ –æ–±—ä–µ–∫—Ç –Ω–µ –∏–º–µ–µ—Ç —Ç–≤–∏–Ω–æ–≤.
     * @param target –ü—Ä–æ–≤–µ—Ä—è–µ–º—ã–π –æ–±—ä–µ–∫—Ç.
     * @return –ù–∞–ª–∏—á–∏–µ —Ç–≤–∏–Ω–æ–≤ —É –æ–±—ä–µ–∫—Ç–∞.
     */
    static public function has(target:Dynamic):Bool {
        if (target == null)
            return false;

        var arr:Array<Tween> = all[target.tweenTargetID];
        if (arr == null)
            return false;

        var len = arr.length;
        while (len-- != 0) {
            if (arr[len] == null || arr[len].stopped)
                continue;
            return true;
        }
        return false;
    }

    /**
     * –û—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –≤—Å–µ –∞–∫—Ç–∏–≤–Ω—ã–µ —Ç–≤–∏–Ω—ã –Ω–∞ —É–∫–∞–∑–∞–Ω–Ω–æ–º –æ–±—ä–µ–∫—Ç–µ.  
     * –í—ã–∑–æ–≤ —ç—Ç–æ–≥–æ –º–µ—Ç–æ–¥–∞ —ç–∫–≤–∏–≤–∞–ª–µ–Ω—Ç–µ–Ω –≤—ã–∑–æ–≤—É: `pause()` –¥–ª—è –∫–∞–∂–¥–æ–≥–æ
     * –æ—Ç–¥–µ–ª—å–Ω–æ –≤–∑—è—Ç–æ–≥–æ —ç–∫–∑–µ–º–ø–ª—è—Ä–∞: `Tween`, —Ü–µ–ª—å—é –∫–æ—Ç–æ—Ä–æ–≥–æ —è–≤–ª—è–µ—Ç—Å—è
     * —É–∫–∞–∑–∞–Ω–Ω—ã–π –æ–±—ä–µ–∫—Ç: `target`
     * @param target –ê–Ω–∏–º–∏—Ä–æ–≤–∞–Ω–Ω—ã–π –æ–±—ä–µ–∫—Ç.
     */
    static public function stop(target:Dynamic):Void {
        if (target == null)
            return;

        var arr:Array<Tween> = all[target.tweenTargetID];
        if (arr == null)
            return;

        var len = arr.length;
        while (len-- > 0) {
            if (arr[len] == null)
                continue;

            arr[len].si = -1;
        }
        NativeJS.delete(all, target.tweenTargetID);
    }

    /**
     * –û—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏–µ –≤—Å–µ—Ö —Ç–≤–∏–Ω–æ–≤ –≥–ª–æ–±–∞–ª—å–Ω–æ.
     */
    static public function stopAll():Void {
        var key:Dynamic = null;
        Syntax.code('for({0} in {1}) {', key, all);
            var arr:Array<Tween> = all[key];
            var len = arr.length;
            while (len-- > 0) {
                if (arr[len] == null)
                    continue;
    
                arr[len].si = -1;
            }
        Syntax.code('}');
        all = {};
    }

    /**
     * –û–±–Ω–æ–≤–∏—Ç—å –∞–Ω–∏–º–∞—Ü–∏—é –≤—Å–µ—Ö —Ç–≤–∏–Ω–æ–≤.  
     * –≠—Ç–æ—Ç –º–µ—Ç–æ–¥ –≤—ã–∑—ã–≤–∞–µ—Ç—Å—è –∞–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫–∏ —á–µ—Ä–µ–∑ –∑–∞–¥–∞–Ω–Ω—ã–µ –ø—Ä–æ–º–µ–∂—É—Ç–∫–∏
     * –≤—Ä–µ–º–µ–Ω–∏: `interval` (–ü–æ —É–º–æ–ª—á–∞–Ω–∏—é). –ï—Å–ª–∏ –∏–Ω—Ç–µ—Ä–≤–∞–ª –Ω–µ –∑–∞–¥–∞–Ω, –≤—ã
     * –¥–æ–ª–∂–Ω—ã —Å–∞–º–æ—Å—Ç–æ—è—Ç–µ–ª—å–Ω–æ –≤—ã–∑—ã–≤–∞—Ç—å —ç—Ç–æ—Ç –º–µ—Ç–æ–¥. –≠—Ç–æ—Ç –º–µ—Ç–æ–¥ –¥–æ–ª–∂–µ–Ω 
     * –≤—ã–∑—ã–≤–∞—Ç—å—Å—è –æ–±—è–∑–∞—Ç–µ–ª—å–Ω–æ, –∏–Ω–∞—á–µ –º–æ–∂–µ—Ç –ø—Ä–æ–∏–∑–æ–π—Ç–∏ —É—Ç–µ—á–∫–∞ –ø–∞–º—è—Ç–∏
     * –∏–∑-–∑–∞ –Ω–∞–∫–æ–ø–ª–µ–Ω–∏—è –Ω–µ–∑–∞–≤–µ—Ä—à—ë–Ω–Ω—ã—Ö —Ç–≤–∏–Ω–æ–≤.
     * @param time –ü—Ä–æ—à–µ–¥—à–µ–µ –≤—Ä–µ–º—è. *(mc)*
     * @return –ë–æ–ª—å—à–µ –Ω–µ—Ç —Ç–≤–∏–Ω–æ–≤ –¥–ª—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è.
     */
    static public function step(time:Float):Bool {
        steps ++;

        var targetID:Dynamic = null;
        var finish:Bool = true;
        Syntax.code('for ({0} in {1}) {', targetID, all); // for in
            var arr:Array<Tween> = all[targetID];
            var i = 0;
            var j = 0;
            while (i < arr.length) {
                var tween = arr[i++];

                // –ü—É—Å—Ç–∞—è —è—á–µ–π–∫–∞ –∏–ª–∏ –Ω–µ –∞–∫—Ç—É–∞–ª—å–Ω—ã–π —Ç–≤–∏–Ω:
                if (tween == null || tween.stopped)
                    continue;

                // –ü—Ä–æ–ø—É—Å–∫ –Ω–æ–≤—ã—Ö —Ç–≤–∏–Ω–æ–≤, –¥–æ–±–∞–≤–ª–µ–Ω–Ω—ã—Ö –≤ —ç—Ç–æ–º —Ü–∏–∫–ª–µ –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è: (–û–±–Ω–æ–≤—è—Ç—Å—è –Ω–∞ —Å–ª–µ–¥—É—é—â–µ–º)
                if (NativeJS.eq(tween.stp, steps)) {
                    if (NativeJS.eq(tween.si, j)) {
                        j ++;
                    }
                    else {
                        tween.si = j;
                        arr[j++] = tween;
                    }
                    continue;
                }

                // –¢–≤–∏–Ω–∏–º:
                tween.update(time);
                if (tween.stopped)
                    continue;

                // –¢–≤–∏–Ω –µ—â—ë –Ω–µ –∑–∞–∫–æ–Ω—á–∏–ª—Å—è:
                finish = false;
                if (NativeJS.eq(tween.si, j)) {
                    j ++;
                }
                else {
                    tween.si = j;
                    arr[j++] = tween;
                }
            }
            if (NativeJS.eq(j, 0))
                NativeJS.delete(all, targetID);
            else if (NativeJS.neq(j, i))
                arr.resize(j);
        Syntax.code('}'); // for end

        return finish;
    }
}

/**
 * –û–ø—Ü–∏–π –¥–ª—è –Ω–æ–≤–æ–≥–æ —Ç–≤–∏–Ω–∞.  
 * –ò—Å–ø–æ–ª—å–∑—É–µ—Ç—Å—è –¥–ª—è –ø–µ—Ä–µ–¥–∞—á–∏ –ø–∞—Ä–∞–º–µ—Ç—Ä–æ–≤ –Ω–æ–≤–æ–º—É
 * —ç–∫–∑–µ–º–ø–ª—è—Ä—É —Ç–≤–∏–Ω–∞.
 */
typedef TweenOptions =
{
    /**
     * –°–º–æ—Ç—Ä–∏—Ç–µ –æ–ø–∏—Å–∞–Ω–∏–µ —ç—Ç–æ–≥–æ —Å–≤–æ–π—Å—Ç–≤–∞: `Tween.reversed`  
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false`
     */
    @:optional var reversed:Bool;

    /**
     * –°–º–æ—Ç—Ä–∏—Ç–µ –æ–ø–∏—Å–∞–Ω–∏–µ —ç—Ç–æ–≥–æ —Å–≤–æ–π—Å—Ç–≤–∞: `Tween.loop`  
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `0` *(–û–¥–Ω–æ –ø—Ä–æ–∏–≥—Ä—ã–≤–∞–Ω–∏–µ)*
     */
    @:optional var loop:Int;

    /**
     * –°–º–æ—Ç—Ä–∏—Ç–µ –æ–ø–∏—Å–∞–Ω–∏–µ —ç—Ç–æ–≥–æ —Å–≤–æ–π—Å—Ç–≤–∞: `Tween.bounce`
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false`
     */
    @:optional var bounce:Bool;

    /**
     * –°–º–æ—Ç—Ä–∏—Ç–µ –æ–ø–∏—Å–∞–Ω–∏–µ —ç—Ç–æ–≥–æ —Å–≤–æ–π—Å—Ç–≤–∞: `Tween.speed`
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `1` *(–û–±—ã—á–Ω–∞—è —Å–∫–æ—Ä–æ—Å—Ç—å)*
     */
    @:optional var speed:Float;

    /**
     * –ù–µ –ø—Ä–æ–∏–≥—Ä—ã–≤–∞—Ç—å —Ç–≤–∏–Ω –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é.  
     * –ü–µ—Ä–µ–¥–∞–≤: `true`, –≤—ã –æ—Ç–∫–ª—é—á–∏—Ç–µ –∞–≤—Ç–æ–º–∞—Ç–∏—á–µ—Å–∫–æ–µ
     * –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏–µ —Ç–≤–∏–Ω–∞ —Å—Ä–∞–∑—É –ø–æ—Å–ª–µ –µ–≥–æ —Å–æ–∑–¥–∞–Ω–∏—è.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false` *(–ü—Ä–æ–∏–≥—Ä—ã–≤–∞–µ—Ç—Å—è)*
     */
    @:optional var stopped:Bool;

    /**
     * –ù–∞—á–∞–ª—å–Ω–∞—è –ø–æ–∑–∏—Ü–∏—è –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è. *(0-1)*  
     * –í—ã –º–æ–∂–µ—Ç–µ —É–∫–∞–∑–∞—Ç—å –Ω–∞—á–∞–ª—å–Ω—É—é –ø–æ–∑–∏—Ü–∏—é –≤–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏—è
     * –±–µ–∑ —É—á—ë—Ç–∞ —Ü–∏–∫–ª–æ–≤.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `0` *(–í–æ—Å–ø—Ä–æ–∏–∑–≤–µ–¥–µ–Ω–∏–µ —Å–Ω–∞—á–∞–ª–∞)*
     */
    @:optional var position:Float;

    /**
     * –£–¥–∞–ª–µ–Ω–∏–µ –≤—Å–µ—Ö –¥—Ä—É–≥–∏—Ö –∞–∫—Ç–∏–≤–Ω—ã—Ö —Ç–≤–∏–Ω–æ–≤ —Ü–µ–ª–∏.  
     * –ü—Ä–∏ –ø–µ—Ä–µ–¥–∞—á–µ: `true`, —É–¥–∞–ª—è–µ—Ç —Å —Ü–µ–ª–∏ –≤—Å–µ –¥—Ä—É–≥–∏–µ –∞–∫—Ç–∏–≤–Ω—ã–µ
     * —Ç–≤–∏–Ω—ã. –ò—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–∏–µ —ç—Ç–æ–≥–æ —Å–≤–æ–π—Å—Ç–≤–∞ –∞–Ω–∞–ª–æ–≥–∏—á–Ω–æ –≤—ã–∑–æ–≤—É:
     * `Tween.stop(target)` –ø–µ—Ä–µ–¥ —Å–æ–∑–¥–∞–Ω–∏–µ–º –Ω–æ–≤–æ–≥–æ —Ç–≤–∏–Ω–∞.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false` *(–ù–µ —É–¥–∞–ª—è—Ç—å)*
     */
    @:optional var clear:Bool;

    /**
     * –†–∞–∑–±–æ—Ä –Ω–µ —á–∏—Å–ª–æ–≤—ã—Ö –∑–Ω–∞—á–µ–Ω–∏–π.  
     * - –ï—Å–ª–∏: `true`, –ø—ã—Ç–∞–µ—Ç—Å—è –ø—Ä–∏–≤–µ—Å—Ç–∏ –≤—Å–µ **–Ω–µ —á–∏—Å–ª–æ–≤—ã–µ**
     *   –∑–Ω–∞—á–µ–Ω–∏—è –∫ —á–∏—Å–ª—É.
     * - –ï—Å–ª–∏: `false`, —Ä–∞–∑–±–æ—Ä –Ω–µ –ø—Ä–æ–∏–∑–≤–æ–¥–∏—Ç—Å—è.
     * 
     * –≠—Ç–æ —Å–≤–æ–π—Å—Ç–≤–æ –ø–æ–ª–µ–∑–Ω–æ –¥–ª—è –∞–Ω–∏–º–∞—Ü–∏–∏ —á–∏—Å–µ–ª –≤ —Å—Ç—Ä–æ–∫–æ–≤–æ–º –≤–∏–¥–µ.
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é —Ç–≤–∏–Ω–µ—Ä –Ω–µ –∞–Ω–∏–º–∏—Ä—É–µ—Ç —á–∏—Å–ª–∞ –≤ —Å—Ç—Ä–æ–∫–µ.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false` *(–†–∞–∑–±–æ—Ä –∑–Ω–∞—á–µ–Ω–∏–π –≤—ã–∫–ª—é—á–µ–Ω)*
     */
    @:optional var parse:Bool;

    /**
     * –ú–æ–¥–∏—Ñ–∏–∫–∞—Ç–æ—Ä –∑–Ω–∞—á–µ–Ω–∏—è.  
     * –≠—Ç–∞ —Ñ—É–Ω–∫—Ü–∏—è –±—É–¥–µ—Ç –≤—ã–∑—ã–≤–∞—Ç—å—Å—è –∫–∞–∂–¥—ã–π —Ä–∞–∑ –ø–µ—Ä–µ–¥ —É—Å—Ç–∞–Ω–æ–≤–∫–æ–π
     * –Ω–æ–≤–æ–≥–æ –∑–Ω–∞—á–µ–Ω–∏—è —Ü–µ–ª–∏. –§—É–Ω–∫—Ü–∏—è –ø—Ä–∏–Ω–∏–º–∞–µ—Ç —É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º–æ–µ
     * –∑–Ω–∞—á–µ–Ω–∏–µ –∏ –¥–æ–ª–∂–Ω–∞ –≤–µ—Ä–Ω—É—Ç—å –∏—Ç–æ–≥–æ–≤—ã–π —Ä–µ–∑—É–ª—å—Ç–∞—Ç –¥–ª—è —Ü–µ–ª–∏.
     * –ù–∞ –≤—Ö–æ–¥ –ø–æ–ª—É—á–∞–µ—Ç —Ç–∏–ø –¥–∞–Ω–Ω—ã—Ö, —Å–æ–æ—Ç–≤–µ—Ç—Å—Ç–≤—É—é—â–∏–π –∑–∞—è–≤–ª–µ–Ω–Ω–æ–º—É
     * –ø—Ä–∏ –≤—ã–∑–æ–≤–µ: `Tween.to()`
     * 
     * –≠—Ç–∞ –æ–ø—Ü–∏—è –º–æ–∂–µ—Ç –±—ã—Ç—å —É–¥–æ–±–Ω–∞ –¥–ª—è –∞–Ω–∏–º–∞—Ü–∏–∏ css —Å–≤–æ–π—Å—Ç–≤:
     * `15px` –ø—Ä–∏ —Å–≤—è–∑–∫–µ —Å: `parse=true`
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `null` *(–ú–æ–¥–∏—Ñ–∏–∫–∞—Ç–æ—Ä –∑–Ω–∞—á–µ–Ω–∏—è –Ω–µ –∏—Å–ø–æ–ª—å–∑—É–µ—Ç—Å—è)*
     */
    @:optional var modifier:Dynamic->Dynamic;

    /**
     * –ú–Ω–æ–≥–æ—É—Ä–æ–≤–Ω–µ–≤–∞—è –≤–ª–æ–∂–µ–Ω–Ω–æ—Å—Ç—å.  
     * –ü–æ–∑–≤–æ–ª—è–µ—Ç –∞–Ω–∏–º–∏—Ä–æ–≤–∞—Ç—å –≤–ª–æ–∂–µ–Ω–Ω—ã–µ –æ–±—ä–µ–∫—Ç—ã, –Ω–∞–ø—Ä–∏–º–µ—Ä, —Ç–∞–∫–∏–µ
     * –∫–∞–∫: `Sprite.scale.x` –í –æ–±—ã—á–Ω–æ–º —Ä–µ–∂–∏–º–µ –≤—ã –Ω–µ –º–æ–∂–µ—Ç–µ
     * –∞–Ω–∏–º–∏—Ä–æ–≤–∞—Ç—å –∑–Ω–∞—á–µ–Ω–∏–µ: `x`, –µ—Å–ª–∏ —Ü–µ–ª—å—é —è–≤–ª—è–µ—Ç—Å—è: `Sprite`
     * –ò—Å–ø–æ–ª—å–∑—É—è —ç—Ç—É –æ–ø—Ü–∏—é: `multilevel=true`, —Ç–≤–∏–Ω–µ—Ä –±—É–¥–µ—Ç
     * –ø—ã—Ç–∞—Ç—å—Å—è –ø—Ä–æ—á–∏—Ç–∞—Ç—å –∏ —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –∑–Ω–∞—á–µ–Ω–∏–µ –ø–æ —É–∫–∞–∑–∞–Ω–Ω–æ–º—É
     * –ø—É—Ç–∏ –≤–ª–æ–∂–µ–Ω–Ω–æ—Å—Ç–∏.
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `false` *(–ò—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å –∫–ª—é—á–∏ "–∫–∞–∫ –µ—Å—Ç—å")*
     */
    @:optional var multilevel:Bool;
}

/**
 * –û–ø–∏—Å–∞–Ω–∏–µ –¥–µ–π—Å—Ç–≤–∏—è –≤—ã–ø–æ–ª–Ω—è–µ–º–æ–≥–æ —Ç–≤–∏–Ω–µ—Ä–æ–º.
 */
private typedef Action =
{
    /**
     * –¢–∏–ø –¥–µ–π—Å—Ç–≤–∏—è.
     */
    var type:ActionType;

    /**
     * –ü—Ä–æ–¥–æ–ª–∂–∏—Ç–µ–ª—å–Ω–æ—Å—Ç—å –¥–µ–π—Å—Ç–≤–∏—è –≤ –º–∏–ª–ª–∏—Å–µ–∫—É–Ω–¥–∞—Ö.
     */
    var duration:Float;

    /**
     * –ü–∞—Ä–∞–º–µ—Ç—Ä—ã –¥–ª—è –∞–Ω–∏–º–∞—Ü–∏–∏.
     */
    @:optional var props:Dynamic;

    /**
     * –ö–µ—à –¥–ª—è —Ö—Ä–∞–Ω–µ–Ω–∏—è –ø–∞—Ä–∞–º–µ—Ç—Ä–æ–≤ –¥–µ–π—Å—Ç–≤–∏—è.  
     * –ò—Å–ø–æ–ª—å–∑—É–µ—Ç—Å—è —Ç–≤–∏–Ω–µ—Ä–æ–º –¥–ª—è –∑–∞–ø–æ–º–∏–Ω–∞–Ω–∏—è –Ω–∞—á–∞–ª—å–Ω—ã—Ö –∑–Ω–∞—á–µ–Ω–∏–π.
     */
    @:optional var cache:Dynamic;

    /**
     * –í—Ä–µ–º—è –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è –≤—Å–µ—Ö –ø—Ä–µ–¥—ã–¥—É—â–∏—Ö –±–ª–æ–∫–æ–≤. *(mc)*
     */
    @:optional var prev:Float;

    /**
     * –§—É–Ω–∫—Ü–∏—è –∏–∑–∏–Ω–≥–∞.
     */
    @:optional var ease:EaseFunction;

    /**
     * –§—É–Ω–∫—Ü–∏—è –≤–Ω–µ—à–Ω–µ–≥–æ –≤—ã–∑–æ–≤–∞.
     */
    @:optional var callback:Function;

    /**
     * –ü–∞—Ä–∞–º–µ—Ç—Ä—ã –¥–ª—è –ø–µ—Ä–µ–¥–∞—á–∏ –≤ —Ñ—É–Ω–∫—Ü–∏—é –≤–Ω–µ—à–Ω–µ–≥–æ –≤—ã–∑–æ–≤–∞.
     */
    @:optional var args:Array<Dynamic>;
}

/**
 * –¢–∏–ø –≤—ã–ø–æ–ª–Ω—è–µ–º–æ–≥–æ –¥–µ–π—Å—Ç–≤–∏—è.  
 * –ï–Ω—É–º —Å–æ–¥–µ—Ä–∂–∏—Ç –ø–µ—Ä–µ—á–∏—Å–ª–µ–Ω–∏–µ –≤—Å–µ—Ö –≤–æ–∑–º–æ–∂–Ω—ã—Ö —Ç–∏–ø–æ–≤
 * –¥–µ–π—Å—Ç–≤–∏–π —Ç–≤–∏–Ω–µ—Ä–æ–≤.
 */
private enum abstract ActionType(Int) to Int
{
    /**
     * –û–∂–∏–¥–∞–Ω–∏–µ.
     */
    var WAIT = 0;

    /**
     * –ò–∑–∏–Ω–≥.
     */
    var EASE = 1;

    /**
     * –í—ã–∑–æ–≤ –≤–Ω–µ—à–Ω–µ–π —Ñ—É–Ω–∫—Ü–∏–∏.
     */
    var CALL = 2;
}