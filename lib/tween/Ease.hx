package tween;

/**
 * –§—É–Ω–∫—Ü–∏–π –∏–∑–∏–Ω–≥–æ–≤. üé¢  
 * –°—Ç–∞—Ç–∏—á–µ—Å–∫–∏–π –∫–ª–∞—Å—Å `Ease` –ø—Ä–µ–¥–æ—Å—Ç–∞–≤–ª—è–µ—Ç –¥–æ—Å—Ç—É–ø –∫ –≥–æ—Ç–æ–≤—ã–º
 * —Ñ—É–Ω–∫—Ü–∏—è–º –∏–∑–∏–Ω–≥–æ–≤ –∏ –∫ –∏—Ö —Ä–∞—Å—à–∏—Ä–µ–Ω–Ω—ã–º –∫–æ–Ω—Å—Ç—Ä—É–∫—Ç–æ—Ä–∞–º, –¥–ª—è
 * –ø–æ—Å—Ç—Ä–æ–µ–Ω–∏—è –Ω–æ–≤—ã—Ö.
 * 
 * –§—É–Ω–∫—Ü–∏–π –∏–∑–∏–Ω–≥–æ–≤ –ø–æ–∑–∞–∏–º—Å—Ç–≤–æ–≤–∞–Ω—ã –∏–∑ –±–∏–±–ª–∏–æ—Ç–µ–∫–∏ CreateJS,
 * –∫–æ—Ç–æ—Ä—ã–µ –≤ —Å–≤–æ—é –æ—á–µ—Ä–µ–¥—å –≤–∑—è—Ç—ã —É Robert Penner.
 * 
 * @see TweenJS: https://www.createjs.com/docs/tweenjs/files/tweenjs_Ease.js.html#l39
 */
@:dce
class Ease
{
    /**
     * –õ–∏–Ω–µ–π–Ω–æ–µ –¥–≤–∏–∂–µ–Ω–∏–µ. *(–ü–æ —É–º–æ–ª—á–∞–Ω–∏—é)*
     */
    static public var linear:EaseFunction = function(t) { return t; }

    static public var quadIn:EaseFunction = Ease.getPowIn(2);
    static public var quadOut:EaseFunction = Ease.getPowOut(2);
    static public var quadInOut:EaseFunction = Ease.getPowInOut(2);

    static public var cubicIn:EaseFunction = Ease.getPowIn(3);
    static public var cubicOut:EaseFunction = Ease.getPowOut(3);
    static public var cubicInOut:EaseFunction = Ease.getPowInOut(3);

    static public var quartIn:EaseFunction = Ease.getPowIn(4);
    static public var quartOut:EaseFunction = Ease.getPowOut(4);
    static public var quartInOut:EaseFunction = Ease.getPowInOut(4);

    static public var quintIn:EaseFunction = Ease.getPowIn(5);
    static public var quintOut:EaseFunction = Ease.getPowOut(5);
    static public var quintInOut:EaseFunction = Ease.getPowInOut(5);

    static public var sineIn:EaseFunction = function(t) { return 1-Math.cos(t*Math.PI/2); }
    static public var sineOut:EaseFunction = function(t) { return Math.sin(t*Math.PI/2); }
    static public var sineInOut:EaseFunction = function(t) { return -0.5*(Math.cos(Math.PI*t) - 1); }

    static public var backIn:EaseFunction = Ease.getBackIn(1.7);
    static public var backOut:EaseFunction = Ease.getBackOut(1.7);
    static public var backInOut:EaseFunction = Ease.getBackInOut(1.7);

    static public var circIn:EaseFunction = function(t) { return -(Math.sqrt(1-t*t)- 1); }
    static public var circOut:EaseFunction = function(t) { return Math.sqrt(1-(--t)*t); }
    static public var circInOut:EaseFunction = function(t) {
        if ((t*=2) < 1) return -0.5*(Math.sqrt(1-t*t)-1);
        return 0.5*(Math.sqrt(1-(t-=2)*t)+1);
    };

    static public var bounceIn:EaseFunction = function(t) {
        return 1-Ease.bounceOut(1-t);
    };
    static public var bounceOut:EaseFunction = function(t) {
        if (t < 1/2.75) return (7.5625*t*t);
        if (t < 2/2.75) return (7.5625*(t-=1.5/2.75)*t+0.75);
        if (t < 2.5/2.75) return (7.5625*(t-=2.25/2.75)*t+0.9375);
        return (7.5625*(t-=2.625/2.75)*t +0.984375);
    };
    static public var bounceInOut:EaseFunction = function(t) {
        if (t<0.5) return Ease.bounceIn (t*2) * .5;
        return Ease.bounceOut(t*2-1)*0.5+0.5;
    };

    static public var elasticIn:EaseFunction = Ease.getElasticIn(1,0.3);
    static public var elasticOut:EaseFunction = Ease.getElasticOut(1,0.3);
    static public var elasticInOut:EaseFunction = Ease.getElasticInOut(1,0.3*1.5);

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è, —ç–∫—Å–ø–æ–Ω–µ–Ω—Ü–∏–∞–ª—å–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è.
     * @param pow –ò—Å–ø–æ–ª—å–∑—É–µ–º–∞—è —ç–∫—Å–ø–æ–Ω–µ–Ω—Ç–∞. (–ó–Ω–∞—á–µ–Ω–∏–µ `3` –≤–µ—Ä–Ω—ë—Ç –∫—É–±–∏—á–µ—Å–∫–∏–π –∏–∑–∏–Ω–≥)
     */
    static public function getPowIn(pow:Float):EaseFunction { 
        return function(t) { 
            return Math.pow(t,pow);
        }
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è, —ç–∫—Å–ø–æ–Ω–µ–Ω—Ü–∏–∞–ª—å–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è.
     * @param pow –ò—Å–ø–æ–ª—å–∑—É–µ–º–∞—è —ç–∫—Å–ø–æ–Ω–µ–Ω—Ç–∞. (–ó–Ω–∞—á–µ–Ω–∏–µ `3` –≤–µ—Ä–Ω—ë—Ç –∫—É–±–∏—á–µ—Å–∫–∏–π –∏–∑–∏–Ω–≥)
     */
    static public function getPowOut(pow:Float):EaseFunction {
        return function(t) {
            return 1-Math.pow(1-t,pow);
        }
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è, —ç–∫—Å–ø–æ–Ω–µ–Ω—Ü–∏–∞–ª—å–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è.
     * @param pow –ò—Å–ø–æ–ª—å–∑—É–µ–º–∞—è —ç–∫—Å–ø–æ–Ω–µ–Ω—Ç–∞. (–ó–Ω–∞—á–µ–Ω–∏–µ `3` –≤–µ—Ä–Ω—ë—Ç –∫—É–±–∏—á–µ—Å–∫–∏–π –∏–∑–∏–Ω–≥)
     */
    static public function getPowInOut(pow:Float):EaseFunction {
        return function(t) {
            if ((t*=2)<1) return 0.5*Math.pow(t,pow);
            return 1-0.5*Math.abs(Math.pow(2-t,pow));
        }
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è `backIn` —Ñ—É–Ω–∫—Ü–∏—è.
     * @param amount –°–∏–ª–∞ –ø—Ä–∏–º–µ–Ω–µ–Ω–∏—è.
     */
    static public function getBackIn(amount:Float):EaseFunction {
        return function(t) {
            return t*t*((amount+1)*t-amount);
        }
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è `backOut` —Ñ—É–Ω–∫—Ü–∏—è.
     * @param amount –°–∏–ª–∞ –ø—Ä–∏–º–µ–Ω–µ–Ω–∏—è.
     */
    static public function getBackOut(amount:Float):EaseFunction {
        return function(t) {
            return (--t*t*((amount+1)*t + amount) + 1);
        }
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è `backInOut` —Ñ—É–Ω–∫—Ü–∏—è.
     * @param amount –°–∏–ª–∞ –ø—Ä–∏–º–µ–Ω–µ–Ω–∏—è.
     */
    static public function getBackInOut(amount:Float):EaseFunction {
        amount*=1.525;
        return function(t) {
            if ((t*=2)<1) return 0.5*(t*t*((amount+1)*t-amount));
            return 0.5*((t-=2)*t*((amount+1)*t+amount)+2);
        };
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è `elasticIn` —Ñ—É–Ω–∫—Ü–∏—è.
     * @param amplitude –ê–º–ø–ª–∏—Ç—É–¥–∞.
     * @param period –ü–µ—Ä–∏—É–¥.
     */
    static public function getElasticIn(amplitude:Float, period:Float):EaseFunction {
        var pi2 = Math.PI*2;
        return function(t) {
            if (t==0 || t==1) return t;
            var s = period/pi2*Math.asin(1/amplitude);
            return -(amplitude*Math.pow(2,10*(t-=1))*Math.sin((t-s)*pi2/period));
        };
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è `elasticOut` —Ñ—É–Ω–∫—Ü–∏—è.
     * @param amplitude –ê–º–ø–ª–∏—Ç—É–¥–∞.
     * @param period –ü–µ—Ä–∏—É–¥.
     */
    static public function getElasticOut(amplitude:Float, period:Float):EaseFunction {
        var pi2 = Math.PI*2;
        return function(t) {
            if (t==0 || t==1) return t;
            var s = period/pi2 * Math.asin(1/amplitude);
            return (amplitude*Math.pow(2,-10*t)*Math.sin((t-s)*pi2/period )+1);
        };
    }

    /**
     * –ö–æ–Ω—Ñ–∏–≥—É—Ä–∏—Ä—É–µ–º–∞—è `elasticInOut` —Ñ—É–Ω–∫—Ü–∏—è.
     * @param amplitude –ê–º–ø–ª–∏—Ç—É–¥–∞.
     * @param period –ü–µ—Ä–∏—É–¥.
     */
    static public function getElasticInOut(amplitude:Float, period:Float):EaseFunction {
        var pi2 = Math.PI*2;
        return function(t) {
            var s = period/pi2 * Math.asin(1/amplitude);
            if ((t*=2)<1) return -0.5*(amplitude*Math.pow(2,10*(t-=1))*Math.sin( (t-s)*pi2/period ));
            return amplitude*Math.pow(2,-10*(t-=1))*Math.sin((t-s)*pi2/period)*0.5+1;
        };
    }
}

/**
 * –§—É–Ω–∫—Ü–∏—è –∏–∑–∏–Ω–≥–∞. üèÑ  
 * –í —Å–∞–º–æ–º –ø—Ä–æ—Å—Ç–æ–º –ø–æ–Ω–∏–º–∞–Ω–∏–∏ –ª—é–±–∞—è —Ñ—É–Ω–∫—Ü–∏—è –∏–∑–∏–Ω–≥–∞
 * —ç—Ç–æ –æ–±—ã—á–Ω–∞—è —Ñ—É–Ω–∫—Ü–∏—è: `f(x)`, –ø—Ä–∏–Ω–∏–º–∞—é—â–∞—è –∏
 * –≤–æ–∑–≤—Ä–∞—â–∞—é—â–∞—è –∑–Ω–∞—á–µ–Ω–∏—è –≤ –¥–∏–∞–ø–∞–∑–æ–Ω–µ –æ—Ç: `0` –¥–æ: `1`
 */
typedef EaseFunction = Float->Float;